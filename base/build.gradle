apply plugin: 'com.android.application'
apply from: '../sharedconfigs-include.gradle'

android {

    defaultConfig {
        applicationId "app.androidtesttask"
        versionCode 1
    }

    signingConfigs {
        release {
            Properties rProps = loadSigningProperties('debug_signing.properties')
            storeFile = file(projectDir.canonicalPath + rProps['STORE_FILE'])
            storePassword = rProps['STORE_PASSWORD']
            keyAlias = rProps['KEY_ALIAS']
            keyPassword = rProps['KEY_PASSWORD']
        }
    }

    buildTypes {
        debug {
            multiDexEnabled true
            minifyEnabled false
            signingConfig signingConfigs.release
            crunchPngs false
            debuggable true
        }
        release {
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            debuggable false
            crunchPngs true
            signingConfig signingConfigs.release
            matchingFallbacks = ['release']
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        preDexLibraries = false
        jumboMode = true
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        // Multiple libs contain this file -> cause build error
        exclude 'LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }

    lintOptions {
        disable 'InvalidPackage'
        disable 'GoogleAppIndexingWarning'
        disable 'RestrictedApi'
        abortOnError true
        checkDependencies true
    }

    kapt {
        // Avoid kapt mechanism that replaces every unknown type with NonExistentClass (for better dagger errors)
        correctErrorTypes = true
        javacOptions {
            // Increase the max count of errors from annotation processors.
            // Default is 100.
            option("-Xmaxerrs", 500)
        }
    }
}

dependencies {
    // Network
    implementation project(":network-core")

    // Domain
    implementation project(":domain-core")
    implementation project(":domain-movies")

    // Data
    implementation project(":data-movies")

    // Features
    implementation project(":feature-common")
    implementation project(":feature-movies")

    implementation kotlinDependencies.kotlinStdLib

    implementation androidXDependencies.appCompat
    implementation androidXDependencies.constraintLayout
    implementation androidXDependencies.design
    implementation androidXDependencies.lifecycle
    implementation androidXDependencies.lifecycleExtensions
    implementation androidXDependencies.navigationFragment
    implementation androidXDependencies.navigationUi
    implementation androidXDependencies.androidKtx

    implementation diDependencies.dagger
    kapt diDependencies.daggerCompiler
    implementation diDependencies.daggerAndroid
    implementation diDependencies.daggerAndroidSupport
    kapt diDependencies.daggerAndroidProcessor

    implementation developmentDependencies.stetho
    implementation developmentDependencies.stethoOkHttp

    debugImplementation developmentDependencies.chucker
    betaImplementation developmentDependencies.chucker
    releaseImplementation developmentDependencies.chuckerNoOp
    implementation developmentDependencies.timber
    implementation developmentDependencies.threeTenABP

    implementation dataDependencies.moshi
    implementation dataDependencies.okHttp
    implementation dataDependencies.okHttpInterceptor
    implementation dataDependencies.retrofit
    implementation dataDependencies.retrofitMoshiConverter

    implementation dataDependencies.room
    kapt dataDependencies.roomCompiler

    testImplementation project(":tests-common")
    testImplementation project(":tests-android-common")
}

Properties loadSigningProperties(String propertyFileName) {
    Properties rProps = new Properties()
    def rpFile = file(propertyFileName)
    if (rpFile.canRead()) {
        rProps.load(new FileInputStream(rpFile))

        if (rProps != null && rProps.containsKey('STORE_FILE') && rProps.containsKey('STORE_PASSWORD') &&
                rProps.containsKey('KEY_ALIAS') && rProps.containsKey('KEY_PASSWORD')) {
            return rProps
        } else {
            throw new Exception(propertyFileName + " found but some entries are missing")
        }
    } else {
        throw new FileNotFoundException(propertyFileName + " not found")
    }
}

android.applicationVariants.all { variant ->
    if (variant.buildType.isMinifyEnabled()) {
        variant.assembleProvider.get().doLast {
            copy {
                from variant.mappingFile
                into "${rootDir}/base/mappings"
                rename { String filename -> "mapping-${variant.versionName}.txt" }
            }
        }
    }
}